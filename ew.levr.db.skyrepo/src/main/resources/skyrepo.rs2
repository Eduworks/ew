// Copyright 2015-2016 Eduworks Corporation and other contributing parties.
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of the License at
// http://www.apache.org/licenses/LICENSE-2.0
// Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.

context=#add(a="@",b="context");

query=#object(
	type=#split(obj="@urlRemainder",split="/").getIndex(index="1"),
	id=#split(obj="@urlRemainder",split="/").getIndex(index="2"),
	version=#split(obj="@urlRemainder",split="/").getIndex(index="3")
);

postData=#fileFromDatastream(name="data");

postObject=postData.fileToString().toObject();
postArray=postData.fileToString().toArray();

get=#skyrepoGet(
    type=query.cget(type=""),
    id=query.cget(id=""),
    version=query.cget(version="")
);

tryFormatOutput=#if(operator="@format", operand="nquads",
	eq=#toObject(obj="@get").jsonLdToNQuads(context=#toObject(obj="@get").getByParam(param=context)),
	ne=#if(operator="@format", operand="turtle",
		eq=#toObject(obj="@get").jsonLdToTurtle(context=#toObject(obj="@get").getByParam(param=context)),
		ne=#toObject(obj="@get")
	)
);

get=#if(
	operator=#toObject(obj="@get").count(),
	operand="0",
	eq=#error(code="404",msg="Object not found or you did not supply sufficient permissions to access the object."),
	ne=#if(operator="@format", operand="",
		eq=#toObject(obj="@get"),
		ne=tryFormatOutput
	),
).call(get=get);

save=postObject.skyrepoPut(
    type=query.cget(type=""),
    id=query.cget(id=""),
    version=query.cget(version="")
);

delete=get.skyrepoDelete(
    type=query.cget(type=""),
    id=query.cget(id=""),
    version=query.cget(version="")
);

saveOrGet=#if(
    operator=postData,
    operand="",
    eq=get.displayJson(_collapse="true"),
    ne=save
);

deleteOrOther = #if(
    operator="@methodType",
    operand="DELETE",
    eq=delete,
    ne=saveOrGet
);

data=deleteOrOther;
/data=data;

multiGet=postArray.cforEach(paramName="urlRemainder",op=get,rethrow="false",array="true").displayJson(_collapse="true");

/sky/repo/multiGet=multiGet;

searchParamData = #fileFromDatastream(name="searchParams");
searchParamObj = searchParamData.fileToString().toObject();

search=#skyrepoSearch(
	q=#if(operator="@q",operand="",eq=postData.fileToString(),ne="@q"), 
	size=#if(operator="@size", operand="",
		eq="50",
		ne="@size"
	).ccall(
		size=#if(operator="@size",operand="",eq=searchParamObj.cget(size=""),ne="@size")
	), 
	start=#if(operator="@start", operand="",
		eq="0",
		ne="@start"
	).ccall(
		start=#if(operator="@start",operand="",eq=searchParamObj.cget(start=""),ne="@start")
	), 
	ownership=#if(operator="@ownership", operand="",
		eq="all",
		ne="@ownership"
	).ccall(
		ownership=#if(operator="@ownership",operand="",eq=searchParamObj.cget(ownership=""),ne="@ownership")
	), 
	types=searchParamObj.cget(types="")
);

/sky/repo/search=search;

types = #skyrepoTypes();
/sky/repo/types=types;

admin = #skyrepoAdminList().displayJson(_collapse="true");
/sky/admin = admin;